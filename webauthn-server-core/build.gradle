plugins {
  id 'java-library'
  id 'java-test-fixtures'
  id 'scala'
  id 'info.solidsoft.pitest'
}

description = 'Yubico WebAuthn server core API'

project.ext.publishMe = true

dependencies {

  api(
    project(':yubico-util'),
  )

  implementation(
    addVersion('com.augustcellars.cose:cose-java'),
    addVersion('com.google.guava:guava'),
    addVersion('com.fasterxml.jackson.core:jackson-databind'),
    addVersion('com.upokecenter:cbor'),
    addVersion('org.apache.httpcomponents:httpclient'),
    addVersion('org.bouncycastle:bcprov-jdk15on'),
    addVersion('org.slf4j:slf4j-api'),
  )

  testFixturesApi(
    addVersion('com.augustcellars.cose:cose-java'),
    addVersion('org.scala-lang:scala-library'),
    addVersion('org.scalacheck:scalacheck_2.12'),
  )

  testFixturesImplementation(
    testFixtures(project(':yubico-util')),
    addVersion('com.upokecenter:cbor'),
    addVersion('org.bouncycastle:bcpkix-jdk15on'),
  )

  testImplementation(
    testFixtures(project(':yubico-util')),
    addVersion('com.fasterxml.jackson.datatype:jackson-datatype-jdk8'),
    addVersion('junit:junit'),
    addVersion('org.mockito:mockito-core'),
    addVersion('org.scala-lang:scala-library'),
    addVersion('org.scalatest:scalatest_2.12'),
  )

  testRuntimeOnly(
    addVersion('ch.qos.logback:logback-classic'),
  )
}


jar {
  manifest {
    attributes([
      'Specification-Title': 'Web Authentication: An API for accessing Public Key Credentials',
      'Specification-Version': 'Level 1 Recommendation 2019-03-04',
      'Specification-Vendor': 'World Wide Web Consortium',

      'Specification-Url': 'https://www.w3.org/TR/2019/REC-webauthn-1-20190304/',
      'Specification-Url-Latest': 'https://www.w3.org/TR/webauthn/',
      'Specification-W3c-Status': 'recommendation',
      'Specification-Release-Date': '2019-03-04',

      'Implementation-Id': 'java-webauthn-server',
      'Implementation-Title': 'Yubico Web Authentication server library',
      'Implementation-Version': project.version,
      'Implementation-Vendor': 'Yubico',
      'Implementation-Source-Url': 'https://github.com/Yubico/java-webauthn-server',
      'Git-Commit': getGitCommit(),
    ])
  }
}

pitest {
  pitestVersion = '1.4.9'

  timestampedReports = false
  outputFormats = ['XML', 'HTML']

  avoidCallsTo = [
    'java.util.logging',
    'org.apache.log4j',
    'org.slf4j',
    'org.apache.commons.logging',
    'com.google.common.io.Closeables',
  ]
}

